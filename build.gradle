import com.android.build.gradle.AppPlugin
import com.android.build.gradle.LibraryPlugin

buildscript {
	apply from: "dependencies.gradle"

	repositories {
		google()
		jcenter()
	}

	dependencies {
		classpath "com.android.tools.build:gradle:${versions.gradle}"
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
		classpath "com.google.dagger:hilt-android-gradle-plugin:${versions.hilt}"
	}
}

allprojects {
	repositories {
		google()
		jcenter()
	}
}

subprojects {
	if (project.name == "app") {
		apply plugin: AppPlugin
	} else {
		apply plugin: LibraryPlugin
	}

	apply plugin: 'kotlin-android'
	apply plugin: "kotlin-android-extensions"
	apply plugin: 'kotlin-kapt'

	sourceSets {
		main.java.srcDirs += ["src/main/kotlin"]
		test.java.srcDirs += ["src/test/kotlin"]
		androidTest.java.srcDirs += ["src/androidTest/kotlin"]
	}

	android {
		defaultConfig {
			compileSdkVersion 30
			buildToolsVersion "30.0.2"
			minSdkVersion 21
			targetSdkVersion 30
			versionCode 1
			versionName "1.0"

			testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		}

		compileOptions {
			sourceCompatibility JavaVersion.VERSION_1_8
			targetCompatibility JavaVersion.VERSION_1_8
		}

		kotlinOptions {
			jvmTarget = '1.8'
			useIR = true
		}

		composeOptions {
			kotlinCompilerVersion "${versions.kotlin}"
			kotlinCompilerExtensionVersion "${versions.compose}"
		}

		buildFeatures {
			compose true
		}
	}

	dependencies {
		implementation "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
		implementation "androidx.core:core-ktx:${versions.coreKtx}"
		implementation "androidx.appcompat:appcompat:${versions.appCompat}"

		implementation "androidx.navigation:navigation-fragment-ktx:${versions.navigation}"
		implementation "androidx.navigation:navigation-ui-ktx:${versions.navigation}"

		implementation "androidx.compose.ui:ui:${versions.compose}"
		implementation "androidx.compose.ui:ui-tooling:${versions.compose}"
		implementation "androidx.compose.foundation:foundation:${versions.compose}"
		implementation "androidx.compose.material:material:${versions.compose}"
		implementation "androidx.compose.material:material-icons-core:${versions.compose}"
		implementation "androidx.compose.material:material-icons-extended:${versions.compose}"
		implementation "androidx.compose.runtime:runtime-livedata:${versions.compose}"
	}
}

task clean(type: Delete) {
	delete rootProject.buildDir
}